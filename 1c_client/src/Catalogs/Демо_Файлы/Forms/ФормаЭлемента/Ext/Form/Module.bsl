
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Ключ.Пустая() Тогда
		ФайлЗагружен = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЗначениеЗаполнено(АдресВременногоХранилища) Тогда
		ДД = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
		ТекущийОбъект.Данные = Новый ХранилищеЗначения(ДД, Новый СжатиеДанных(9));
	Иначе
		Если НЕ ФайлЗагружен Тогда
			Сообщить("Файл не загружен");
			Отказ  = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайл(Команда)
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов();
	
	ДополнительныеПараметры = Новый Структура;  	
	ОписаниеОповещения  = Новый ОписаниеОповещения("ОбработатьВыбранныйФайл", ЭтаФорма, ДополнительныеПараметры); 		
	НачатьПомещениеФайлаНаСервер(ОписаниеОповещения,,,, ПараметрыДиалога, УникальныйИдентификатор);     		
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыбранныйФайл(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
 
    Если ОписаниеПомещенногоФайла = Неопределено Тогда
        Возврат;
    КонецЕсли;
	
	АдресВременногоХранилища = ОписаниеПомещенногоФайла.Адрес;			
	
	Файл = ОписаниеПомещенногоФайла.СсылкаНаФайл.Файл;
	
	Объект.Имя = Файл.ИмяБезРасширения;
	Объект.Расширение = СтрЗаменить(Файл.Расширение, ".", "");
	Объект.Наименование = ОписаниеПомещенногоФайла.СсылкаНаФайл.Имя;
	
	ФайлЗагружен = Истина;
	
	Модифицированность = Истина;
	
КонецПроцедуры 

